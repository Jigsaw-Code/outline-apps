plugins {
    id 'com.android.library'
}

// Needed for the composite build to recognize this module as 'org.outline:outline:0.0'.
group = 'org.outline'

android {
    namespace 'org.outline'
    compileSdk 35

    buildFeatures {
        aidl true
    }

    defaultConfig {
        minSdk 22
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // To make this project editable directly in the source tree, we need to load
    // tun2socks.aar from its original path in the third_party directory.  We do
    // this by relative path:
    compileOnly fileTree(dir: '../../../../../../output/client/android',
            include: ['tun2socks.aar'])
    // Note: A flatDir repository might be preferable, but this is not compatible
    // with a composite build (https://github.com/gradle/gradle/issues/911).

    implementation 'io.sentry:sentry-android:2.0.2'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'androidx.annotation:annotation:1.9.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
